apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'com.google.gms.google-services'

android {
    compileSdkVersion project.compileSdkVersion
    buildToolsVersion "30.0.3"
    defaultConfig {
        applicationId "dev.cicdandroid"
        minSdkVersion project.minSdkVersion
        targetSdkVersion project.compileSdkVersion
        versionName generateVersionName()
        versionCode generateVersionCode()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField 'String', "API_KEY", API_KEY

    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "env"

    // If you need to add more flavors, consider using flavor dimensions.
    productFlavors {
        // dev branch (dev variant)
        dev {
            dimension "env"
            applicationIdSuffix = ".dev"
        }
        // master branch (qa variant)
        qa {
            dimension "env"
            applicationIdSuffix = ".qa"
        }
        // release branch (prod variant)
        prod {
            dimension "env"
        }
    }
    buildFeatures {
        viewBinding = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation "com.google.android.material:material:1.4.0"
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"
    implementation "com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.2"
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    androidTestImplementation 'androidx.test:core:1.4.0'
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"

    //Firebase
    // Import the Firebase BoM
    implementation platform('com.google.firebase:firebase-bom:28.2.0')
    // Add the dependency for the Firebase SDK for Google Analytics
    // When using the BoM, don't specify versions in Firebase dependencies
    implementation 'com.google.firebase:firebase-analytics-ktx'


    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.ext:truth:1.4.0'
    androidTestImplementation 'com.google.truth:truth:1.1.3'
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"
    androidTestImplementation 'androidx.test.ext:junit-ktx:1.1.3'
}

private Integer generateVersionCode() {
    return project.minSdkVersion * 1000 + project.versionMajor * 100 +
            project.versionMinor * 10 + project.versionPatch
}

private String generateVersionName() {
    String versionName = "$project.versionMajor.$project.versionMinor.$project.versionPatch"
    return versionName
}

